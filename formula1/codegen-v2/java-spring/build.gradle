plugins {
    id 'java'
    id 'maven-publish'
    id 'org.hidetake.swagger.generator' version '2.19.1'
}

group = 'com.developer618.demo'
version = '0.1-SNAPSHOT'

repositories {
    mavenLocal()
    mavenCentral()
}

publishing {
    publications {
        mvaneJava(MavenPublication) {
            from components.java
        }
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web:2.6.1'
    implementation 'javax.validation:validation-api:2.0.1.Final'
    implementation 'io.swagger:swagger-annotations:1.6.4'
    swaggerCodegen 'io.swagger:swagger-codegen-cli:2.4.24'
}

swaggerSources {
    f1 {
        inputFile = file("$rootDir/formula1/contract/openapi-V2-f1.yaml")
        code {
            language = 'spring'
            configFile = file("$projectDir/java-spring-config.json")
            components = ['models', 'apis']
            dependsOn validation

            // https://github.com/int128/gradle-swagger-generator-plugin/issues/221
            jvmArgs = ['--add-opens=java.base/java.util=ALL-UNNAMED']
        }
    }
}

compileJava.dependsOn swaggerSources.f1.code
sourceSets.main.java.srcDir "${swaggerSources.f1.code.outputDir}/src/main/java"
sourceSets.main.resources.srcDir "${swaggerSources.f1.code.outputDir}/src/main/resources"